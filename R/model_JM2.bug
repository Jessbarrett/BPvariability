# Longitudinal sub-model: Standard mixed effects model with random intercept and subject-specific residual variance
# Survival sub-model: Piecewise constant baseline hazard and association with random intercept and random residual SD

data{
	for(i in 1:Nsurv){
		ones[i]<-1
	}
}

model{

## Longitudinal process
	for(j in 1:Nobs){
		y[j]~dnorm(BP[longid[j]],tauBPSD[longid[j]])
	}

## Survival process
# i specifies the row of the survival dataset, i.e. the individual and the time period
	for(i in 1:Nsurv){

		## Hazard (constant within each time period)
		h[i]<-exp(eta[period[i]]+alpha1*BP[survid[i]]+alpha2*BPSD[survid[i]])

		## Integrated hazard for individual i from start time to end time of time period
		H[i]<-h[i]*(end[i]-start[i])
		
		## Survival function
		S[i]<-exp(-H[i])

		## Probability density function
		f[i]<-h[i]*S[i]

		## Likelihood for survival data using the ones trick
		ones[i]~dbern(p[i])
		p[i]<-L[i]

		## Likelihood contribution for period period[i], individual id[i]
		L[i]<-pow(f[i],event[i])*pow(S[i],1-event[i])
	}

	## Random-effects
	for(i in 1:N){
		BP[i] ~ dnorm(muBP, tauBP)

		tauBPSD[i] <- pow(BPSD[i], -2)
		BPSD[i] <- exp(logBPSD[i])
	# Conditional normal distribution for logBPSD (conditional on BP)
		logBPSD[i] ~ dnorm(mulogBPSD.knowingBP[i], taulogBPSD.knowingBP)
		
		mulogBPSD.knowingBP[i] <- mulogBPSD + sdlogBPSD/sdBP*rho*(BP[i] - muBP)
	}

	## Priors
	muBP ~ dnorm(0,1.0E-4)
	tauBP <- pow(sdBP, -2)
	sdBP ~ dunif(0, 100)
	
	for(k in 1:nk){
		eta[k]~dnorm(0,1.0E-4)
	}
	
	alpha1~dnorm(0,1.0E-4)
	alpha2~dnorm(0,1.0E-4)
	
	mulogBPSD ~ dnorm(0,1.0E-4)
	taulogBPSD <- pow(sdlogBPSD, -2)
	sdlogBPSD ~ dunif(0, 100)
	taulogBPSD.knowingBP <- taulogBPSD*1/(1-pow(rho,2))
	
	rho ~ dunif(-1,1)
}


